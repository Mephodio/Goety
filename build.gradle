//file:noinspection InconsistentLineSeparators
plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')

            property 'mixin.env.disableRefMap', 'true'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            arg '-mixin.config=goety.mixins.json'

            mods {
                goety {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'mixin.env.disableRefMap', 'true'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            arg '-mixin.config=goety.mixins.json'

            mods {
                goety {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.disableRefMap', 'true'

            args '--mod', 'goety', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            arg '-mixin.config=goety.mixins.json'

            mods {
                goety {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "goety.refmap.json"
}

repositories {
    maven {
        name = "CurseForge"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Curios"
        url = "https://maven.theillusivec4.top"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    flatDir {
        dirs 'runtime-mods'
    }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    //JEI
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    //Curios
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curio_version}+${minecraft_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curio_version}+${minecraft_version}")

    //Patchouli
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}")

    //Testing Only
    runtimeOnly fg.deobf("curse.maven:jade-324717:4768593")
    runtimeOnly fg.deobf("curse.maven:yeetus-635427:4680832")
    runtimeOnly fg.deobf("curse.maven:jeed-532286:4599236")
    runtimeOnly fg.deobf("curse.maven:worldedit-225608:4586218")
    runtimeOnly fg.deobf("curse.maven:mob-battle-mod-268746:4748521")
    runtimeOnly fg.deobf("curse.maven:ydms-custom-camera-view-592245:4601457")
    runtimeOnly fg.deobf("curse.maven:tslateffectslib-700382:4744867")
    runtimeOnly fg.deobf("curse.maven:tslatentitystatus-tes-830420:4746281")
    runtimeOnly fg.deobf("curse.maven:kiwi-303657:4688568")
    runtimeOnly fg.deobf("curse.maven:wiki-zoomer-383277:4576732")
    runtimeOnly fg.deobf("curse.maven:bagus-lib-866533:5147658")
    runtimeOnly fg.deobf("curse.maven:enchant-with-mob-913523:5098324")
    runtimeOnly fg.deobf("curse.maven:spark-361579:4738952")
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "goety.mixins.json"
        ])
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
